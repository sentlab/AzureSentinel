// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as CiscoSEGEvent and specifying Legacy Category.
// This function maps Cisco SEG Consolidated Event Logs to normalization schema (https://docs.microsoft.com/azure/sentinel/normalization-schema).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. CiscoSEGEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
CommonSecurityLog
| where DeviceEventClassID =~ 'ESA_CONSOLIDATED_LOG_EVENT'
| parse-kv AdditionalExtensions as (startTime:string, endTime:string, ESAAttachmentDetails:string) with (pair_delimiter=';', kv_delimiter='=')
| parse-kv AdditionalExtensions as (ESAMID:int, ESAICID:int, ESADCID:int, ESAAMPVerdict:string, ESAASVerdict:string, ESAAVVerdict:string, ESACFVerdict:string, ESADLPVerdict:string, ESADMARCVerdict:string, ESAFriendlyFrom:string, ESAGMVerdict:string, ESAMailFlowPolicy:string, ESAMFVerdict:string, ESAOFVerdict:string, ESAHeloDomain:string, ESAReplyTo:string, ESASDRDomainAge:string, ESASPFVerdict:string, sourceHostName:string, sourceAddress:string, ESATLSInConnStatus:string, ESATLSInProtocol:string, ESATLSOutConnStatus:string) with (pair_delimiter=';', kv_delimiter='=')
| parse AdditionalExtensions with * "ESAGMVerdict" ESAGMVerdict";" *
| extend NetworkDirection = case(CommunicationDirection == '0', 'Incoming', 'Outgoing')
| parse ESAAttachmentDetails with * "'fileHash': '" FileHashSha256"'}," *
| project-rename EventStartTime = startTime
			   , EventEndTime = endTime
			   , EventVendor = DeviceVendor
               , EventProduct = DeviceProduct
               , EventId = DeviceEventClassID
               , EventSeverity = LogSeverity
               , DvcAction = DeviceAction
               , DvcIpAddr = DeviceAddress
               , EventMessage = Message
               , EventProductVersion = DeviceVersion
               , SerialNumber = DeviceExternalID
               , DvcInboundInterface = DeviceInboundInterface
               , DvcOutboundInterface = DeviceOutboundInterface
               , DstUserName = DestinationUserName
               , SrcUserName = SourceUserName
               , MailPolicy = DeviceCustomString1
               , SrcGeoCountry = DeviceCustomString2
               , ThreatCategory = DeviceCustomString3
               , EventOriginalUid = DeviceCustomString4
               , MailLanguage = DeviceCustomString5
               , SdrRepScore = DeviceCustomString6
               , SbrsScore = DeviceCustomFloatingPoint1
| project-away Activity
             , DeviceCustomString1Label
             , DeviceCustomString2Label
             , DeviceCustomString3Label
             , DeviceCustomString4Label
             , DeviceCustomString5Label
             , DeviceCustomString6Label
             , DeviceCustomFloatingPoint1Label
             , AdditionalExtensions