// Usage Instructions: 
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as StealthwatchEvent and specifying Legacy Category.
// This function maps Digital Guargian DLP events to normalization schema (https://docs.microsoft.com/azure/sentinel/normalization-schema).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. StealthwatchEvent | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
Syslog
| where SyslogMessage has 'Stealthwatch'
| extend EventVendor = 'Cisco'
| extend EventProduct = 'Stealthwatch'
| parse-kv SyslogMessage as (start:string, end:string, alarmID:string, alarmSev:string, alarmStatus:string, cat:string, domain:string, dst:string, dstPort:string, msg:string, proto:string, sourceHG:string, sourceHostSnapshot:string, src:string, flowCollectorIP:string, flowCollectorName:string, sourceUser:string, targetUser:string, argetHG:string, targetHostname:string, targetHostSnapshot:string) with (pair_delimiter='|', kv_delimiter='=')
| parse SyslogMessage with * "argetHG=" argetHG"|" *
| parse SyslogMessage with * "targetHG=" targetHG"|" *
| extend DstGeoCountry = iff(SyslogMessage contains "argetHG",argetHG, targetHG)
| extend EventStartTime=todatetime(start)
| extend EventEndTime=todatetime(end)
| project-rename EventOriginalUid=alarmID
                , EventSeverity=alarmSev
                , EventStatus=alarmStatus
                , EventType=cat
                , SrcDvcDomain=domain
                , DstIpAddr=dst
                , DstPortNumber=dstPort
                , EventMessage=msg
                , Protocol=proto
                , SrcGeoCountry=sourceHG
                , SrcHostSnapshot=sourceHostSnapshot
                , SrcIpAddr=src
                , DvcIpAddr=flowCollectorIP
                , DvcHostname=flowCollectorName
                , SrcUserName=sourceUser
                , DstUserName=targetUser
                , DstDvcHostname=targetHostname
                , DstHostSnapshot=targetHostSnapshot
| project-away start
             , end
             , SyslogMessage
			 , argetHG
			 , targetHG
