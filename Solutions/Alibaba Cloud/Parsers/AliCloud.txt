// Usage Instructions:
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name as AliCloud and specifying Legacy Category.
// This function maps Alibaba Cloud events to Microsoft Sentinel Information Model (ASIM) (https://docs.microsoft.com/azure/sentinel/normalization).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. AliCloud | take 10).
// Reference : Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
AliCloud_CL
| extend EventVendor = 'Alibaba Cloud',
         EventProduct = 'Alibaba Cloud',
         EventCount = 1,
         CreatedAt = unixtime_seconds_todatetime(timestamp_d)
| extend d=parse_json(contents_event_s)
| evaluate bag_unpack(d)
| extend ContentTopic = contents___topic___s,
        SourceName = source_s,
        ContentTagPackId = contents___tag_____pack_id___s
|project-away   timestamp_d,
                contents_event_s,
                contents___topic___s,
                source_s,
                contents___tag_____pack_id___s