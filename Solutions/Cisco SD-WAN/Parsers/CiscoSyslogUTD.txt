// Usage Instruction :
// Paste below query in log analytics, click on Save button and select as Function from drop down by specifying function name and alias (e.g. CiscoSyslogUTD).
// Function usually takes 10-15 minutes to activate. You can then use function alias from any other queries (e.g. CiscoSyslogUTD | take 10).
// References :
// Using functions in Azure monitor log queries : https://docs.microsoft.com/azure/azure-monitor/log-query/functions
// Tech Community Blog on KQL Functions : https://techcommunity.microsoft.com/t5/Azure-Sentinel/Using-KQL-functions-to-speed-up-analysis-in-Azure-Sentinel/ba-p/712381
//
let CiscoSyslogUTD_view = view() {
    Syslog
    | where SyslogMessage matches regex @"\[\*\*\].*\[\*\*\].*\[\*\*\][^-]*.*->"
    | extend FieldList = extract_all(@"]\s(?P<action>[^\s[]*)\s\[\S*\s[^{]*{(?P<protocol>[^\s}]*)}\s(?P<src_ip>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}):(?P<src_port>\d{1,5})\s->\s(?P<dest_ip>\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}):(?P<dest_port>\d{1,5})", dynamic(['action', 'protocol', 'src_ip', 'src_port', 'dest_ip', 'dest_port']), SyslogMessage)
    | extend
        Action = tostring(FieldList[0][0]),
        Protocol = tostring(FieldList[0][1]),
        SourceIP = tostring(FieldList[0][2]),
        SourcePort = tostring(FieldList[0][3]),
        DestinationIP = tostring(FieldList[0][4]),
        DestinationPort = tostring(FieldList[0][5])
    | extend UTDWebfilter = extract(@"\sUTD\sWebFilter\s(?P<utd_webfilter>[^\s]*)", 1, SyslogMessage)
    | extend SignatureId = replace_string(extract(@"\[(?P<raw_signature_id>\d+\:\d+)\:\d+\]",1,SyslogMessage),':','-')
    | extend FileInspectionLogs = extract(@"UTD\sAMP\sDISPOSITION\s(?P<utd_amp_disposition>[^\s]*)\s", 1, SyslogMessage)
    | extend BracketData = SyslogMessage 
    | extend NonBracketData = SyslogMessage
    | extend 
        EventVendor="CiscoSD-WAN",
        EventProduct="Syslog_UTD"
    | project
        EventVendor,
        EventProduct,
        Host = HostName,
        TimeGenerated = EventTime,
        Action,
        Protocol,
        SourceIP,
        SourcePort,
        DestinationIP,
        DestinationPort,
        UTDWebfilter,
        FileInspectionLogs,
        BracketData,
        NonBracketData,
        SignatureId,
        SyslogMessage
    | parse-kv BracketData as (['Hostname'] : string, ['System_IP']: string, ['Instance_ID']: string, ['Classification']: string, ['Priority']: string, ['POLICY']: string, ['URL']: string, ['Category']: string, ['Reputation']: string) with (regex=@"\s\[([^\[\]:]*):\s([^\]]*)\]")
    | project-away BracketData
    | parse-kv NonBracketData as (['SHA']: string, ['Malware']: string, ['Filename']: string, ['Filetype']: string) with (regex=@"[\s|\{]([\w.:\/]+):\s([\w.]*)")
    | project-away NonBracketData
};
CiscoSyslogUTD_view