// Title:           Fortigate Fortiweb parser
// Author:          Microsoft
// Version:         1.0
// Last Updated:    14/12/2022
// Comment:         
//
// DESCRIPTION:
// This parser is based on Attack log type of Fortiweb appliance for more details refe - //https://help.fortinet.com/fweb/560/Content/FortiWeb/fortiweb-admin/logging.htm#:~:text=The%20FortiWeb%20appliance%20will%20store,Critical%2C%20Alert%2C%20and%20Emergency.
// LOG SAMPLES:
// This parser assumes the raw log are formatted as follows:
// 
// Jul 14 13:11:42 syslog.fortiweb-cloud.com syslogng[9] CEF:0||||N/A|IP Protection: IP in black list was blocked|Critical|cat=attack rt=2022-07-14T08:11:19-05:00 cs1=My App 1 cs1Label=appName //src=167.220.238.23 spt=52169 dpt=443 cs2=India cs2Label=srcCountry app=https/tls1.3 act=Monitor cs3=IP Protection cs3Label=mainType cs4=Black IP List cs4Label=subType cs5=Critical cs5Label=threatLevel cn1=200 //cn1Label=threatWeight dhost=soartesting.net request=/ requestMethod=GET requestClientApplication=Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/103.0.0.0 Safari/537.36 //cn2=1535 cn2Label=length cn3=N/A cn3Label=signatureID cs6=N/A cs6Label=owaspTop10 msg=IP Protection: IP in black list was blocked
CommonSecurityLog
| project-away EventType
| where DeviceVendor =~ 'Fortinet' and DeviceProduct has 'Fortiweb'
| extend EventVendor = 'Fortinet'
| extend EventProduct = 'Fortiweb'
| extend EventSchemaVersion = 0.2
| extend EventCount = 1
| extend DeviceCustomNumber1 = coalesce(column_ifexists("FieldDeviceCustomNumber1", long(null)),DeviceCustomNumber1),
         DeviceCustomNumber2 = coalesce(column_ifexists("FieldDeviceCustomNumber2", long(null)),DeviceCustomNumber2),
         DeviceCustomNumber3 = coalesce(column_ifexists("FieldDeviceCustomNumber3", long(null)),DeviceCustomNumber3),
         ExternalID = coalesce(column_ifexists("ExtID", ""),tostring(ExternalID))
| extend Threatweight = iff(DeviceCustomNumber1Label =~ "threatWeight",DeviceCustomNumber1,long(null))
         ,Length = iff(DeviceCustomNumber2Label =~ "length",DeviceCustomNumber2,long(null))
         ,SignatureID = iff(DeviceCustomNumber3Label =~ "signatureID",DeviceCustomNumber3,long(null))
         ,AppName = iff(DeviceCustomString1Label =~ "appName",DeviceCustomString1,'')
         ,srcCountry = iff(DeviceCustomString2Label =~ "srcCountry",DeviceCustomString2,'')
         ,mainType = iff(DeviceCustomString3Label =~ "mainType",DeviceCustomString3,'')
         ,subType = iff(DeviceCustomString4Label =~ "subType",DeviceCustomString4,'')
         ,ThreatLevel = iff(DeviceCustomString5Label =~ "threatLevel",DeviceCustomString5,'')
         ,owaspTop10 = iff(DeviceCustomString6Label =~ "owaspTop10",DeviceCustomString6,'')
| parse-kv AdditionalExtensions as (cat:string) with (pair_delimiter=';', kv_delimiter='=')
| extend EventEndTime = todatetime(ReceiptTime),
         EventType = coalesce(column_ifexists("DeviceEventCategory", ""),cat)
| project-rename EventProductVersion=DeviceVersion
                , EventOriginalType=DeviceEventClassID
                , EventSeverity=LogSeverity
                , EventMessage=Message
                , DstPortNumber=DestinationPort
                , DstIpAddr=DestinationIP
                , DstDvcHostname=DestinationHostName
                , DstUserName=DestinationUserName
                , DvcIpAddr=DeviceAddress
                , DvcAction=DeviceAction
                , DvcHostname=DeviceName
                , DstMacAddr=DestinationMACAddress
                , NetworkApplicationProtocol=Protocol
                , SrcPortNumber=SourcePort
                , SrcIpAddr=SourceIP
                , SrcMacAddr=SourceMACAddress
                , EventId=ExternalID
                , SrcDvcHostname=SourceHostName
                , EventSubType=Activity
| project-away AdditionalExtensions
            , ReceiptTime
            , cat
            , Device*